# darc prompt theme

prompt_darc_help () {
  cat <<'EOF'

  prompt darc [<color1> [<color2> [<color3> [<color4> [<color5>]]]]]

  defaults are red, yellow, green, blue, and white, respectively.

EOF
}

prompt_darc_setup () {
  setopt noxtrace noksharrays localoptions
  prompt_opts=(cr percent subst)
  local -a pcc
  local -A pc

  autoload -Uz vcs_info add-zsh-hook

  add-zsh-hook precmd prompt_darc_precmd

  pcc[1]=${1:-${${SSH_CLIENT+'yellow'}:-'red'}}
  pcc[2]=${2:-'yellow'}
  pcc[3]=${3:-'green'}
  pcc[4]=${4:-'blue'}
  pcc[5]=${5:-'white'}

  typeset -ga zle_highlight
  zle_highlight[(r)default:*]=default:$pcc[2]

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format "%B%F{$pcc[1]}...%f%b"
  zstyle ':prezto:module:editor:info:keymap:primary' format "%B%F{$pcc[1]}❯%f%b"
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format "%F{$pcc[1]}♺%f"
  zstyle ':prezto:module:editor:info:keymap:alternate' format "%B%F{$pcc[1]}❮%f%b"

  PROMPT='
${(e)$(prompt_darc_build)}${editor_info[keymap]} '
  PS2='%(4_.\.)%3_> %E'
  SPROMPT='zsh: correct %F{$pcc[2]}%R%f to %F{$pcc[3]}%r%f [nyae]? '
}

prompt_darc_build () {
  setopt noxtrace noksharrays localoptions
  local -a pcc
  local -A pc
  local p_date p_tty p_plat p_ver p_userpwd p_apm p_shlvlhist p_rc p_end p_win p_vcs

  pcc[1]=${1:-${${SSH_CLIENT+'yellow'}:-'red'}}
  pcc[2]=${2:-'yellow'}
  pcc[3]=${3:-'green'}
  pcc[4]=${4:-'blue'}
  pcc[5]=${5:-'white'}

  pc['\[']="%F{$pcc[1]}%S["
  pc['\]']="%F{$pcc[1]}]%s"
  pc['<']="%F{$pcc[1]}%B<"
  pc['>']="%F{$pcc[1]}>%b"

  p_date="$pc['\[']%F{$pcc[2]}%D{%A %F %T %Z}$pc['\]']"

  p_userpwd="$pc['<']%F{$pcc[3]}%n@%m%F{$pcc[5]}:%F{$pcc[4]}%d$pc['>']"
  p_vcs="%(2v.%U%2v%u.)"

  p_shlvlhist="%fzsh%(2L./$SHLVL.) %B%h%b "
  p_rc="%(?..[%?%1v] )"
  p_end="%f%B%#%b "

  print "$p_date
$p_userpwd
$p_shlvlhist$p_rc$p_vcs$p_end "
}

prompt_darc_precmd () {
  setopt noxtrace noksharrays localoptions
  local exitstatus=$?
  local git_dir git_ref

  psvar=()
  [[ $exitstatus -ge 128 ]] && psvar[1]=" $signals[$exitstatus-127]" ||
	psvar[1]=""

  [[ -o interactive ]] && jobs -l

  vcs_info
  [[ -n $vcs_info_msg_0_ ]] && psvar[2]="$vcs_info_msg_0_"
}

prompt_darc_setup "$@"
