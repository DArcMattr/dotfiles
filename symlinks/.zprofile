#!/usr/bin/env zsh
# Executes commands at login pre-zshrc.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
# Execute code that does not affect the current session in the background.

{
  # Compile the completion dump to increase startup speed.
  zcompdump="${ZDOTDIR:-$HOME}/.zcompdump"
  if [[
    -s "$zcompdump"
    && (
      ! -s "${zcompdump}.zwc"
      || "$zcompdump" -nt "${zcompdump}.zwc"
    )
  ]]; then
    zcompile "$zcompdump"
  fi
} &!

# Less

# Set the default Less options.
# Mouse-wheel scrolling has been disabled by -X (disable screen clearing).
# Remove -X and -F (exit if the content fits on one screen) to enable it.
# export LESS='-F -g -i -M -R -S -w -X -z-4'

_cols=$(tput cols 2>/dev/null || echo 80)

export PAGER='less'
export LESS="-~EFiIMQRsx2X"
export LESSCHARSET=utf-8
export MANWIDTH="$(( 96 > ${_cols:-80} ? ${_cols:-80} : 96 ))"
export PYTHONSTARTUP="${HOME}/dotfiles/helpers/pythonstartup.py"
export LSCOLORS="ExFxCxDxBxEgEdAbAgAcAd"
export HGEDITOR="${HOME}/dotfiles/helpers/hgeditor"
export CLICOLOR=YES
export CLICOLOR_FORCE=YES
export ANSIBLE_NOCOWS=1
export AUTOSSH_PORT=0
export MANPAGER='nvim +Man!'
export FZF_DEFAULT_COMMAND="rg --files --hidden --follow --glob '!.git'"

# Editors
if type "nvim" > /dev/null; then
  EDITOR=$(which nvim)
elif type "vim" > /dev/null; then
  EDITOR=$(which vim)
else
  EDITOR=$(which vi)
fi
export EDITOR
export VISUAL=${EDITOR}

# Browsers
if [[ "$OSTYPE" == darwin* ]]; then
  BROWSER='open'
else
  BROWSER='xdg-open'
fi
export BROWSER

# Set the Less input preprocessor.
# Try both `lesspipe` and `lesspipe.sh` as either might exist on a system.
if (( $#commands[(i)lesspipe(|.sh)] )); then
  export LESSOPEN="| /usr/bin/env $commands[(i)lesspipe(|.sh)] %s 2>&-"
fi

LOCALPROFILE="${HOME}/dotfiles/.profile.${HOST}"

if [ -r "${LOCALPROFILE}" ]; then
  source "${LOCALPROFILE}"
fi

# Generated by running `xdg-user-dirs` and `xdg-user-dirs-update`
if [ -r "${XDG_CONFIG_HOME}/user-dirs.dirs" ]; then
  source "${XDG_CONFIG_HOME}/user-dirs.dirs"
fi
